import org.gradle.util.VersionNumber

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        consumerProguardFiles 'proguard-rules.pro'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            consumerProguardFiles 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

def safeExtGet(prop, fallback) {
    def ver = rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    if (VersionNumber.parse(fallback) > VersionNumber.parse(ver)) {
        throw new Exception("Version mismatch for $prop: $ver found, at least $fallback required")
    }
    return ver
}

dependencies {
    // Applicaster SDK
    if (!findProject(':applicaster-android-sdk') || project.hasProperty('forceClosedDependencies')) {
        def sdkVersion = safeExtGet('applicaster_android_sdk_core_version', '8.0.0')
        api ("com.applicaster:applicaster-android-sdk-core:${sdkVersion}")
    }
    else {
        api project(':applicaster-android-sdk')
    }
    implementation 'com.adobe.marketing.mobile:sdk-core:1.8.2'
    implementation 'com.adobe.marketing.mobile:analytics:1.2.8'
    implementation 'com.adobe.marketing.mobile:media:2.1.2'
    implementation 'com.adobe.marketing.mobile:userprofile:1.1.0'
}
